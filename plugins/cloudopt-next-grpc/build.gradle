buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.15'
    }
}

apply plugin: 'com.google.protobuf'

sourceSets {
    main {
        proto {
            srcDir "src/main/proto"
        }
        java {
            srcDirs "src/main/java", "generated/main/java", "generated/main/grpc", "generated/main/vertx"
        }
        kotlin {
            srcDirs "src/main/kotlin", "generated/main/kotlin"
        }
    }

    test {
        proto {
            srcDir "src/test/proto"
        }
        java {
            srcDirs "src/test/java", "generated/test/java", "generated/test/grpc", "generated/test/vertx"
        }
        kotlin {
            srcDirs "src/test/kotlin", "generated/test/kotlin"
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.2.0'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.25.0"
        }
        vertx {
            artifact = "io.vertx:vertx-grpc-protoc-plugin:${rootProject.property('vertx_version')}"
        }
    }
    generateProtoTasks.generatedFilesBaseDir = "generated"
    generateProtoTasks {
        all()*.plugins {
            grpc
            vertx
        }
    }
}

dependencies {
    api project(":cloudopt-next-web")
    api("com.google.protobuf:protobuf-java:3.6.1")
    api("io.grpc:grpc-stub:${rootProject.property('grpc_version')}")
    api("io.grpc:grpc-protobuf:${rootProject.property('grpc_version')}")
    api("javax.annotation:javax.annotation-api:1.3.1")
    implementation("io.vertx:vertx-grpc:${rootProject.property('vertx_version')}")
}

description = "Cloudopt Next GRPC"